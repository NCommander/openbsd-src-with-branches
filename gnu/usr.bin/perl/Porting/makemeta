#!./perl -w
# this script must be run by the current perl to get perl's version right
#
# Create a META.yml file in the current directory. Must be run from the
# root directory of a perl source tree.

use strict;
use warnings;
use lib "Porting";

use File::Basename qw( dirname );

my $file = "META.yml";
die "$0: will not override $file, delete it first.\n" if -e $file;

use Maintainers qw(%Modules get_module_files get_module_pat);

my @CPAN  = grep { $Modules{$_}{CPAN} } keys %Modules;
my @files = ('lib/unicore/mktables', 'TestInit.pm',
	     'Porting/Maintainers.pm', 'Porting/perldelta_template.pod',
	     map { get_module_files($_) } @CPAN);
my @dirs  = ('cpan', 'win32', grep { -d $_ && $_  !~ /^cpan/ } map { get_module_pat($_) } @CPAN);

my %dirs;
@dirs{@dirs} = ();

my $files = join '', map { "    - $_\n" }
  grep {
    my $d = $_;
    while(($d = dirname($d)) ne "."){
      last if exists $dirs{$d};
    }

    # if $d is "." it means we tried every parent dir of the file and none
    # of them were in the private list
    
    $d eq "."; 
  }
  sort { lc $a cmp lc $b } @files;

my $dirs  = join '', map { "    - $_\n" } sort { lc $a cmp lc $b } @dirs;

open my $fh, ">$file" or die "Can't open $file: $!";

print $fh <<"EOI";
name: perl
version: $]
abstract: Practical Extraction and Report Language
author: perl5-porters\@perl.org
license: perl
resources:
  homepage: http://www.perl.org/
  bugtracker: http://rt.perl.org/perlbug/
  license: http://dev.perl.org/licenses/
  repository: http://perl5.git.perl.org/
distribution_type: core
generated_by: $0
no_index:
  directory:
$dirs
  file:
$files
EOI

close $fh;

